PREFIX diseasome: <http://www4.wiwiss.fu-berlin.de/diseasome/resource/diseasome/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX stat: <http://purl.org/net/biordfmicroarray/stat#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX biordf: <http://purl.org/net/biordfmicroarray/ns#> 
PREFIX neurolex: <http://neurolex.org/wiki/Category:> 
PREFIX doid: <http://purl.org/obo/owl/DOID#> 

SELECT DISTINCT  ?diseaseName ?geneLabel ?geneName WHERE {

	#Retrieve a list of overexpressed genes in the entorhinal cortex of AD patients
	{
		?experimentSet	dct:isPartOf	?microarray_experiment	;
				biordf:has_input_value	?sampleList ;
				biordf:differentially_expressed_gene	 ?gene ;
				biordf:has_ouput_value ?foldChange .

		?sampleList	biordf:derives_from_region	?brainRegion ;
			biordf:patients_have_disease	?alzheimers .

		?gene	rdfs:label ?geneLabel ;
		biordf:name	   ?geneName .

		?foldChange	rdf:value	?foldChangeValue ;
			stat:p_value ?pval .

		#Apply filters to constrain the amount of results
		FILTER (xsd:float(?foldChangeValue) > 0)
		FILTER (xsd:float(?pval) < 0.001 )
		FILTER (?brainRegion = neurolex:Entorhinal_cortex ) 
		FILTER (?alzheimers = doid:DOID_10652 )
	}

	#Find most recently updated SPARQL endpoint that contains information about genes and diseases.
	GRAPH <SOURCES> {
		?source	rdf:type	void:Dataset ;
		void:sparqlEndpoint	?srvc ;
		dct:issued		?issued  ;
		dct:subject		diseasome:diseases ;
		dct:subject		diseasome:genes .

		OPTIONAL {
		?source1	rdf:type	void:Dataset ;
		void:sparqlEndpoint	?srvc2 ;
		dct:issued	?issued2 ;
		dct:subject	diseasome:diseases ; 
		dct:subject	diseasome:genes . 
		FILTER (?issued2 > ?issued) 
		}
		FILTER (BOUND(?srvc2))
	}

	#Get associated diseases from most recently updated Diseasome server.
	SERVICE ?srvc {
		?diseasomeGene rdfs:label ?geneLabel .
		?disease diseasome:associatedGene ?diseasomeGene.
		?disease rdfs:label ?diseaseName .
	}
}
